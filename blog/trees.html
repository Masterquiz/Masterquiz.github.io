<!DOCTYPE html>
<html lang="en">
  <head>
    <meta name="viewport" content="width=device-width, initial-scale=1.0" />
    <title>Trees - Blog | Master Quiz</title>

    <link rel="stylesheet" href="/css/blog/blog_apl.css" />

    <script src="/js/script.js"></script>
    <script src="/js/blog/index.js" defer></script>
    <script src="/js/apl/syntaxHighlight.js" defer></script>

    <script src="/js/apl/apl.js"></script>
    <script
      src="https://cdnjs.cloudflare.com/ajax/libs/pako/2.0.4/pako.min.js"
      integrity="sha512-EAKzSKex+PXC0U9OG13r1059ysjrjkJEpZoONCnZa0mBROY28iBOOxZSErUVw1LzLr2+U5PhR7zPCPKidUVJqg=="
      crossorigin="anonymous"
      referrerpolicy="no-referrer"></script>
  </head>

  <body>
    <div id="header"></div>

    <main>
      <h1>Trees</h1>

      <ol>
        <li>The aim is to fill the grid with 1 tree per row, column and area.</li>
        <li>There can't be adjacent trees.</li>
        <li>
          If side length is greater or equal to 9 you have to put 2 trees per rows/column and area
        </li>
      </ol>

      <h2>Solver</h2>
      <p>
        How to encode the matrix? An area is formed with equals numbers and in the output 1s
        corresponds to a tree.
      </p>
      <p>In the explanation I'll use this matrix:</p>

      <div class="example"></div>

      <pre class="code">
<code>
  ⎕← mat ← 5 5⍴1 1 2 3 4 1 1 2 2 4 5 1 2 4 4 5 5 5 4 4 5 5 5 5 5
  ⎕← sol ← 5 5⍴0 0 0 1 0 1 0 0 0 0 0 0 1 0 0 0 0 0 0 1 0 1 0 0 0
</code>
      </pre>
      <p>The solver has to do three things:</p>
      <ol>
        <li>Put a tree</li>
        <li>Remove invalid puzzles</li>
        <li>Mark cells where there can't be a tree</li>
      </ol>
      <p>
        Doing this for every area will create multiple possible areas each iteration and we will end
        up having the solution(s)
      </p>
      <p>First of all let's create a matrix to fill</p>
      <pre class="code">
<code>
  ⎕← x ← {0}¨mat
</code>
      </pre>
      <p>
        Let's for example fill the Area 1. Every area should have one tree, so let's put a 1 in
        every cell.
      </p>
      <pre class="code">
<code>
  ⎕← area ← 1=mat
  ⎕← vec  ← ⍸area
  ⎕← res  ← {1@⍵⊢x}¨1⊂vec
</code>
      </pre>
      <p>
        When there's only a tree per row/column/area, there can't be an invalid matrix, so let's
        skip the second step for now.
      </p>
      <p>To mark a cell where we can't place a tree we use a ¯1</p>
      <pre class="code">
<code>
  ⎕← adj ← {×⍵-¯1 ¯1↓1 1↓⊃∨/⊃,/4 ¯4↑¨⊂,¯1 0 1∘.⊖¯1 0 1⌽¨⊂0(,∘⌽∘⍉⍣4)1=⍵}¨res
  ⎕← cr  ← {(⊢-⊢<(1≤+/)∘.∨1≤+⌿)1=⍵}¨res
  ⎕← (⊢-(⊂area)∧0∘=) adj⌊cr
</code>
      </pre>
      <p>Now let's start to write a function that does all this steps with two arguments:</p>
      <ul>
        <li>- ⍵: Matrix to fill</li>
        <li>- ⍺: Matrix to solve and area to fill</li>
      </ul>
      <p>
        Notice that when searching for possible cells in an area I added a (0≤⍵) to take only valid
        cells
      </p>

      <pre class="code">
<code>
  f ← {
    x ← ⍵ ⋄ (m y) ← ⍺
    area ← y=m
    vec  ← ⍸(0≤⍵)∧area
    res  ← {1@⍵⊢x}¨1⊂vec
    adj  ← {×⍵-¯1 ¯1↓1 1↓⊃∨/⊃,/4 ¯4↑¨⊂,¯1 0 1∘.⊖¯1 0 1⌽¨⊂0(,∘⌽∘⍉⍣4)1=⍵}¨res
    cr   ← {(⊢-⊢<(1≤+/)∘.∨1≤+⌿)1=⍵}¨res
    (⊢-(⊂area)∧0∘=)adj⌊cr
  }
</code>
      </pre>

      <p>
        We can use this function to view all possible configurations for the Area 1. And executing
        it multiple time we can calculate the solution
      </p>
      <pre class="code">
<code>
  ⎕← ⊃,/ (mat 1)∘f ¨⊂{0}¨mat
  ⎕← ⊃,/(mat 3)∘f¨⊃,/(mat 2)∘f¨⊃,/(mat 1)∘f¨⊂{0}¨mat
  ⎕← ⊃,/(mat 5)∘f¨⊃,/(mat 4)∘f¨⊃,/(mat 3)∘f¨⊃,/(mat 2)∘f¨⊃,/(mat 1)∘f¨⊂{0}¨mat
</code>
      </pre>

      <p></p>

      <p>
        With a few modification we can generalise the funtion to accept larger inputs that requires
        n trees per row/column/area.
      </p>
      <p>
        But before we have to write the function to check if a matrix is valid or not; in fact, when
        n = 2 we can reach three trees in a case similar to the following: the first row cannot be
        unmarked cause it has only one tree, but in the next iteration it place two trees in the
        first row, so it becomes an invalid matrix.
      </p>

      <pre class="code">
<code>
  ⎕← 9 9⍴1 1 1 0 0 2 2 2 0 1 1 1 0 0 2 2 2 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0
  ⎕← 9 9⍴1 ¯1 ¯1 ¯1 0 0 0 0 0 ¯1 ¯1 1 ¯1 0 0 0 0 0 0 ¯1 ¯1 ¯1 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0
  ⎕← 9 9⍴1 ¯1 ¯1 ¯1 0 1 0 1 0 ¯1 ¯1 1 ¯1 0 0 0 0 0 0 ¯1 ¯1 ¯1 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0
</code>
      </pre>

      <p>We have to check three thing:</p>
      <ul>
        <li>
          - The last added trees can't be adjacent (This is the reason we save in a variable the
          combination of positions in vec)
        </li>
        <li>- There can't be more than n trees in the same row</li>
        <li>- There can't be more than n trees in the same column</li>
      </ul>
      <pre class="code">
<code>
  check ← {
    ⍺⍺=1: ∧/⍺⍺≥(+/,+⌿)⍵>0
    (∧/⍺⍺≥(+/,+⌿)⍵>0)>(0 1)(1 1)(1 0)∊⍨|-/⍺
  }
</code>
      </pre>

      <p>
        We can optimise the function so it chose to fill the smallest area, doing this will reduce
        the number of possible configuration. We can now pass to f, instead of the area to fill, the
        number of tree per row/column/area
      </p>
      <pre class="code">
<code>
  ⎕← ({⊃⍤⍋⊢∘≢⌸⍵}⌷∪)0~⍨,mat×0=x
</code>
</pre>

      <p>
        Now we just have to check if there are at least n+1 cells free in an area and to take the
        configuration of position with length next
      </p>
      <pre class="code">
<code>
  cmat ← {⊖⊃⍪/{k,¨⍪\1+⍵}⍣⍺⊢(⊂⍉⍪⍬),d⍴⊂0 0⍴k←⌽⍳1+d←⍵-⍺}

  n ← 1 ⋄ ⎕← ↓vec[n cmat≢vec]
  n ← 2 ⋄ ⎕← ↓vec[n cmat≢vec]
</code>
      </pre>

      <p>The ending functions would be these</p>
      <pre class="code">
<code>
  f ← {
    x ← ⍵
    n ← ⍺
    y ← ({⊃⍤⍋⊢∘≢⌸⍵}⌷∪)0~⍨,⍺⍺×0=x
    area ← y=⍺⍺
    n>≢vec ← ⍸(0≤⍵)∧area: ⍬
    vec ← ↓vec[n cmat≢vec]
    res ← {1@⍵⊢x}¨vec
    res ← res/⍨vec(n check)¨res
    adj ← {×⍵-¯1 ¯1↓1 1↓⊃∨/⊃,/4 ¯4↑¨⊂,¯1 0 1∘.⊖¯1 0 1⌽¨⊂0(,∘⌽∘⍉⍣4)1=⍵}¨res
    cr ← {(⊢-⊢<(n≤+/)∘.∨n≤+⌿)1=⍵}¨res
    (⊢-(⊂area)∧0∘=)adj⌊cr
  }

  solver ← {
    n ← 4 9⍸≢ m ← ⍵
    1={⊃,/n (m f)¨⍵/⍨0∊¨⍵}⍣(⌈/,⍵)⊢⊂{0}¨⍵
  }
</code>
      </pre>

      <pre class="code">
<code>
  mat5x5 ← 5 5⍴1 1 2 3 4 1 1 2 2 4 5 1 2 4 4 5 5 5 4 4 5 5 5 5 5
  mat9x9 ← 9 9⍴1 1 1 1 2 3 3 3 3 1 1 1 1 2 2 3 3 3 1 1 1 2 2 2 4 3 5 1 1 1 6 2 4 4 4 5 7 7 6 6 6 6 4 5 5 7 7 7 6 6 8 5 5 5 7 7 7 7 8 8 8 5 5 9 7 7 8 8 8 8 8 5 9 9 9 9 8 8 8 8 5

  ⎕← solver mat5x5
  ⎕← solver mat9x9
</code>
      </pre>

      <h2>Creator</h2>
      <pre class="code">
<code>
  createTree ← {
    mat ← {(1+⌈/,⍵)@(⊂(?∘⍴⊃⊢)∘⍸¨(~+/,⍥⊂+⌿)×⍵)⊢⍵}⍣⍵⊢⍵ ⍵⍴0
    {⍵+(0=⍵)×¯1 ¯1↓1 1↓⊃⊃⌈/2 4 5 6 8⌷¨⊂,¯1 0 1∘.⊖¯1 0 1⌽¨⊂0(,∘⌽∘⍉⍣4)⍵}⍣≡mat
  }

  ⎕← createTree 9
</code>
      </pre>

      <pre class="code">
<code>
  creator ← {
    4:: ∇⍵
    n←1+8<≢m←createTree ⍵
    (1=≢ ∧ (∧/1∘∊¨)) solver m: m
    ∇⍵
  }

  ⎕← creator 9
</code>
      </pre>

      <h2>Code</h2>
      <pre class="code">
<code>
  mat ← 9 9⍴1 1 1 1 2 3 3 3 3 1 1 1 1 2 2 3 3 3 1 1 1 2 2 2 4 3 5 1 1 1 6 2 4 4 4 5 7 7 6 6 6 6 4 5 5 7 7 7 6 6 8 5 5 5 7 7 7 7 8 8 8 5 5 9 7 7 8 8 8 8 8 5 9 9 9 9 8 8 8 8 5

  cmat ← {⊖⊃⍪/{k,¨⍪\1+⍵}⍣⍺⊢(⊂⍉⍪⍬),d⍴⊂0 0⍴k←⌽⍳1+d←⍵-⍺}

  check ← {
    ⍺⍺=1: ∧/⍺⍺≥(+/,+⌿)⍵>0
    (∧/⍺⍺≥(+/,+⌿)⍵>0)>(0 1)(1 1)(1 0)∊⍨|-/⍺
  }

  f ← {
    x ← ⍵
    n ← ⍺
    y ← ({⊃⍤⍋⊢∘≢⌸⍵}⌷∪)0~⍨,⍺⍺×0=x
    area ← y=⍺⍺
    n>≢vec ← ⍸(0≤⍵)∧area: ⍬
    vec ← ↓vec[n cmat≢vec]
    res ← {1@⍵⊢x}¨vec
    res ← res/⍨vec(n check)¨res
    adj ← {×⍵-¯1 ¯1↓1 1↓⊃∨/⊃,/4 ¯4↑¨⊂,¯1 0 1∘.⊖¯1 0 1⌽¨⊂0(,∘⌽∘⍉⍣4)1=⍵}¨res
    cr  ← {(⊢-⊢<(n≤+/)∘.∨n≤+⌿)1=⍵}¨res
    (⊢-(⊂area)∧0∘=)adj⌊cr
  }

  solver ← {
    n ← 4 9⍸≢ m ← ⍵
    1={⊃,/n (m f)¨⍵/⍨0∊¨⍵}⍣(⌈/,⍵)⊢⊂{0}¨⍵
  }

  createTree ← {
    mat ← {(1+⌈/,⍵)@(⊂(?∘⍴⊃⊢)∘⍸¨(~+/,⍥⊂+⌿)×⍵)⊢⍵}⍣⍵⊢⍵ ⍵⍴0
    {⍵+(0=⍵)×¯1 ¯1↓1 1↓⊃⊃⌈/2 4 5 6 8⌷¨⊂,¯1 0 1∘.⊖¯1 0 1⌽¨⊂0(,∘⌽∘⍉⍣4)⍵}⍣≡mat
  }

  creator ← {
    4:: ∇⍵
    n←1+8<≢m←createTree ⍵
    (1=≢ ∧ (∧/1∘∊¨)) solver m: m
    ∇⍵
  }
</code>
      </pre>

      <script
        src="https://utteranc.es/client.js"
        repo="Masterquiz/masterquiz.github.io"
        issue-term="pathname"
        theme="preferred-color-scheme"
        crossorigin="anonymous"
        async></script>
    </main>

    <div id="footer"></div>
  </body>

  <script>
    (async () => {
      [state, size, hash] = (
        await exTryAPL('mat ← 5 5⍴1 1 2 3 4 1 1 2 2 4 5 1 2 4 4 5 5 5 4 4 5 5 5 5 5', true)
      ).splice(0, 3);
    })();

    table = document.createElement('table');
    tbody = document.createElement('tbody');
    table.appendChild(tbody);
    document.querySelector('.example').appendChild(table);

    [
      [3, 2, 3, 3, 3],
      [1, 0, 1, 2, 1],
      [3, 1, 1, 3, 0],
      [1, 2, 2, 1, 0],
      [1, 0, 0, 2, 2],
    ].map((item, i) => {
      const tr = document.createElement('tr');
      item.map((x, j) => {
        const td = document.createElement('td');
        if (x & 1) td.style.borderLeft = '2px solid #f1fa8c';
        if (x & 2) td.style.borderTop = '2px solid #f1fa8c';

        if ([3, 0, 2, 4, 1][i] === j) {
          td.style.background = 'url(/img/logic_games/trees.png)';
          td.style.backgroundSize = '69%';
          td.style.backgroundRepeat = 'no-repeat';
          td.style.backgroundPosition = 'center';
        }
        tr.appendChild(td);
      });
      tbody.appendChild(tr);
    });
  </script>
</html>
